plugins {
    id 'com.android.library'
}
android {
    compileSdk 34
    namespace "com.trtc.uikit.livekit"
    defaultConfig {
        minSdkVersion 21
        versionName "1.0"
        versionCode 1
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api project(':tuilivekit:livestreamcore')
    api project(':tuilivekit:voiceroomcore')
    api project(':tuilivekit:component:common')
    api project(':tuilivekit:component:barrage')
    api project(':tuilivekit:component:gift')
    api project(':tuilivekit:component:audioeffect')
    api project(':tuilivekit:component:dashboard')
    api project(':tuilivekit:component:audiencelist')
    api project(':tuilivekit:component:music')
    api project(':tuilivekit:component:roominfo')

    api 'androidx.appcompat:appcompat:1.3.1'
    api 'com.google.android.material:material:1.4.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.1'
    api 'androidx.gridlayout:gridlayout:1.0.0'
    api "com.google.code.gson:gson:2.8.6"
    api 'com.google.android.flexbox:flexbox:3.0.0'

    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    def projects = this.rootProject.getAllprojects().stream().map { project -> project.name }.collect()
    api projects.contains("tuicore") ?
            project(':tuicore') : "com.tencent.liteav.tuikit:tuicore:latest.release"

    api rootProject.getProperties().containsKey("roomEngineSdk") ? rootProject.ext.roomEngineSdk : "io.trtc.uikit:rtc_room_engine:latest.release"
    api rootProject.getProperties().containsKey("liteavSdk") ? rootProject.ext.liteavSdk : "com.tencent.liteav:LiteAVSDK_Professional:latest.release"
    api rootProject.getProperties().containsKey("imSdk") ? rootProject.ext.imSdk : "com.tencent.imsdk:imsdk-plus:latest.release"

    if (projects.contains("tceffectplayerkit")) {
        api project(':tceffectplayerkit')
    }
    if (projects.contains("beautykit")) {
        api project(':beautykit')
    }
    if (projects.contains("tebeautykit")) {
        api project(':tebeautykit')
    }
}