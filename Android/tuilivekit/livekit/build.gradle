plugins {
    id 'com.android.library'
}
android {
    compileSdk 34
    namespace "com.trtc.uikit.livekit"
    defaultConfig {
        minSdkVersion 21
        versionName "1.0"
        versionCode 1
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/res-common',
                    'src/main/res-anchor-prepare',
                    'src/main/res-anchor-boardcast',
                    'src/main/res-audience-list',
                    'src/main/res-audio-effect',
                    'src/main/res-beauty',
                    'src/main/res-gift',
                    'src/main/res-room-info',
                    'src/main/res-livelist',
            ]
        }
    }
}

dependencies {
    api project(':tuilivekit:component:barrage')

    api 'androidx.appcompat:appcompat:1.3.1'
    api 'com.google.android.material:material:1.4.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.1'
    api 'androidx.gridlayout:gridlayout:1.0.0'
    api "com.google.code.gson:gson:2.8.6"
    api 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.github.yyued:SVGAPlayer-Android:2.6.1'

    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    def projects = this.rootProject.getAllprojects().stream().map { project -> project.name }.collect()
    api projects.contains("tuicore") ?
            project(':tuicore') : "com.tencent.liteav.tuikit:tuicore:latest.release"

    api rootProject.getProperties().containsKey("roomEngineSdk") ? rootProject.ext.roomEngineSdk : "io.trtc.uikit:rtc_room_engine:3.1.0.824"
    api rootProject.getProperties().containsKey("liteavSdk") ? rootProject.ext.liteavSdk : "com.tencent.liteav:LiteAVSDK_Professional:12.5.0.17567"
    api rootProject.getProperties().containsKey("imSdk") ? rootProject.ext.imSdk : "com.tencent.imsdk:imsdk-plus:8.6.7019"
    api rootProject.getProperties().containsKey("liveStreamCore") ? rootProject.ext.liveStreamCore : "io.trtc.uikit:live-stream-core:3.1.0.946"

    if (projects.contains("tceffectplayerkit")) {
        api project(':tceffectplayerkit')
    }
    if (projects.contains("beautykit")) {
        api project(':beautykit')
    }
    if (projects.contains("tebeautykit")) {
        api project(':tebeautykit')
    }
}