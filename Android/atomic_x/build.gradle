plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}
android {
    compileSdk rootProject.compileSdkVersion
    namespace = "io.trtc.tuikit.atomicx"
    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        versionName "1.0"
        versionCode 1
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def javaVersion = JavaVersion.VERSION_1_8
    def gradleMajorVersion = gradle.gradleVersion.tokenize('.')[0].toInteger()
    if (gradleMajorVersion >= 8) {
        javaVersion = JavaVersion.VERSION_17
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/res-karaoke',
            ]
        }
    }
}

dependencies {
    api 'androidx.appcompat:appcompat:1.3.1'
    api 'com.google.android.material:material:1.12.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.1'
    api "com.google.code.gson:gson:2.8.6"
    api 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.github.yyued:SVGAPlayer-Android:2.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    def projects = this.rootProject.getAllprojects().stream().map { project -> project.name }.collect()
    api projects.contains("tuicore") ? project(':tuicore') : "com.tencent.liteav.tuikit:tuicore:latest.release"

    api rootProject.getProperties().containsKey("roomEngineSdk") ? rootProject.ext.roomEngineSdk : "io.trtc.uikit:rtc_room_engine:3.4.0.1306"
    api rootProject.getProperties().containsKey("atomicxCoreSdk") ? rootProject.ext.atomicxCoreSdk : "io.trtc.uikit:atomicx-core:3.4.0.1307"
    api rootProject.getProperties().containsKey("liteavSdk") ? rootProject.ext.liteavSdk : "com.tencent.liteav:LiteAVSDK_Professional:12.8.0.19279"
    api rootProject.getProperties().containsKey("common") ? rootProject.ext.common : "io.trtc.uikit:common:3.3.0.1194"

}