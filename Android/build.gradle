import org.gradle.util.VersionNumber

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        VersionNumber currentGradleVersion = VersionNumber.parse(gradle.gradleVersion)
        if (currentGradleVersion.major >= 8) {
            classpath 'com.android.tools.build:gradle:8.0.2'
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.0"
        } else {
            classpath 'com.android.tools.build:gradle:4.2.1'
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10"
        }
    }
}

allprojects {
    repositories {
        flatDir {
            dirs project(':app').file('libs')
            if (rootProject.getChildProjects().containsKey("tceffectplayer")) {
                dirs rootProject.getChildProjects().get("tceffectplayer").file('libs')
            }
        }
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    liteavSdk = "com.tencent.liteav:LiteAVSDK_Professional:latest.release"
    roomEngineSdk = "io.trtc.uikit:rtc_room_engine:latest.release"
    imSdk = "com.tencent.imsdk:imsdk-plus:latest.release"
}